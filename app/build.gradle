plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

apply from: rootProject.file("gradle/git-tag-version.gradle")

android {
    namespace 'no.nordicsemi.android.blinky'
    compileSdk android_api_version

    defaultConfig {
        applicationId "no.nordicsemi.android.nrfblinky"
        minSdk android_min_api_version
        targetSdk android_api_version
        versionCode getVersionCodeFromTags()
        versionName getVersionNameFromTags()
        resConfigs "en"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file('../keystore')
            storePassword System.env.KEYSTORE_PSWD
            keyAlias System.env.KEYSTORE_ALIAS
            keyPassword System.env.KEYSTORE_KEY_PSWD
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    /**
     * There is an issue with recomposition performance when data objects doesn't belong to the module
     * when composition is enabled, because those objects cannot be properly compared for the changes.
     * Better to leave enabled for all modules.
     */
    composeOptions {
        kotlinCompilerExtensionVersion compose_compiler_version
    }

    buildFeatures {
        compose true
    }
}

dependencies {
    implementation project(':spec')
    implementation project(':scanner')
    implementation project(':control')
    implementation project(':transport_ble')

    implementation "no.nordicsemi.android.common:theme:1.0.23"
    implementation "no.nordicsemi.android.common:navigation:1.0.23"

    implementation "androidx.compose.ui:ui:$compose_version"
    // Tooling support (Previews, etc.)
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    // Foundation (Border, Background, Box, Image, Scroll, shapes, animations, etc.)
    implementation "androidx.compose.foundation:foundation:$compose_version"
    // Material 3
    implementation "androidx.compose.material3:material3:1.0.0-beta03"
    // Integration with activities
    implementation "androidx.activity:activity-compose:1.6.0"

    // Tests
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    // Hilt and Dagger
    implementation 'com.google.dagger:hilt-android:2.43.2'
    kapt 'com.google.dagger:hilt-compiler:2.43.2'
    implementation "androidx.hilt:hilt-navigation-compose:1.0.0"
    kapt "androidx.hilt:hilt-compiler:1.0.0"
}